
# once this file is loaded, we are found
set(AcMake_FOUND YES)

# determine where our various files are
if(AcMake_SOURCE_DIR)
    # we are in an integrated build
    set(ACMAKE_IS_IMPORTED NO)
    set(ACMAKE_IS_INTEGRATED YES)

    set(ACMAKE_MODULES_DIR ${AcMake_SOURCE_DIR}/modules)

    # extend the module path to make AcMake loadable
    list(INSERT CMAKE_MODULE_PATH 0 ${ACMAKE_MODULES_DIR})
else(AcMake_SOURCE_DIR)
    # we are being used from build or install
    set(ACMAKE_IS_IMPORTED YES)
    set(ACMAKE_IS_INTEGRATED NO)

    set(_CMAKE_TO_PREFIX "../../..")
    get_filename_component(ACMAKE_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
    get_filename_component(ACMAKE_PREFIX    "${ACMAKE_CMAKE_DIR}/${_CMAKE_TO_PREFIX}" ABSOLUTE)

    if(EXISTS "${ACMAKE_PREFIX}/CMakeFiles")
        set(ACMAKE_MODULES_DIR   "@AcMake_SOURCE_DIR@/modules")

        # extend the module path to make AcMake loadable
        list(INSERT CMAKE_MODULE_PATH 0 ${ACMAKE_MODULES_DIR})

        # add modules in build dir
        list(INSERT CMAKE_MODULE_PATH 0 "${ACMAKE_PREFIX}/@ACMAKE_MODULES_SUB@")

    else(EXISTS "${ACMAKE_PREFIX}/CMakeFiles")
        set(ACMAKE_MODULES_DIR   "${ACMAKE_PREFIX}/@ACMAKE_MODULES_SUB@")

        # extend the module path to make AcMake loadable
        list(INSERT CMAKE_MODULE_PATH 0 ${ACMAKE_MODULES_DIR})

    endif(EXISTS "${ACMAKE_PREFIX}/CMakeFiles")
endif(AcMake_SOURCE_DIR)

# load the build system
include(AcMake)
